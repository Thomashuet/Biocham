; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "@PACKAGE_NAME@"
#define MyAppVerName "@PACKAGE_STRING@"
#define MyAppPublisher "INRIA - Contraintes"
#define MyAppURL "http://contraintes.inria.fr/BIOCHAM/"
#define MyAppExeName "BiochamGUI.exe"
#define MyAppDir "."

[Setup]
AppName={#MyAppName}
AppVerName={#MyAppVerName}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\{#MyAppName}
DefaultGroupName={#MyAppName}
SourceDir={#MyAppDir}
LicenseFile=COPYING
OutputBaseFilename={#MyAppName}Setup
OutputDir=.
Compression=lzma
SolidCompression=yes
PrivilegesRequired=none
;ChangesEnvironment=yes
ChangesAssociations=yes

[Languages]
Name: "eng"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"
Name: "quicklaunchicon"; Description: "{cm:CreateQuickLaunchIcon}"; GroupDescription: "{cm:AdditionalIcons}"

[Files]
Source: "gui\*.jar"; DestDir: "{app}\gui"; Flags: ignoreversion
Source: "biocham.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: ".dll\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "gui\{#MyAppExeName}"; DestDir: "{app}\gui"; Flags: ignoreversion
Source: "index.html"; DestDir: "{app}"; Flags: ignoreversion
Source: "DOC\*.html"; DestDir: "{app}\DOC"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "DOC\*.pdf"; DestDir: "{app}\DOC"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "DOC\*.gif"; DestDir: "{app}\DOC"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "DOC\*.png"; DestDir: "{app}\DOC"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "EXAMPLES\*"; Excludes: ".svn"; DestDir: "{app}\EXAMPLES"; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\gui\{#MyAppExeName}"; WorkingDir: "{app}\gui"; Parameters: """{app}\gui"""
Name: "{group}\{#MyAppName} Documentation"; Filename: "{app}\DOC\{#MyAppName}.html";
Name: "{group}\{#MyAppName} Examples"; Filename: "{app}\EXAMPLES\";
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"; Parameters: "{app}"
Name: "{userdesktop}\{#MyAppName}"; Filename: "{app}\gui\{#MyAppExeName}"; WorkingDir: "{app}\gui"; Parameters: """{app}\gui"""; Tasks: desktopicon
Name: "{userappdata}\Microsoft\Internet Explorer\Quick Launch\{#MyAppName}"; Filename: "{app}\gui\{#MyAppExeName}"; WorkingDir: "{app}\gui"; Parameters: """{app}\gui"""; Tasks: quicklaunchicon

;[Run]
;Filename: "{app}\gui\{#MyAppExeName}"; WorkingDir: "{app}\gui"; Parameters: "{app}\gui"; Description: "{cm:LaunchProgram,{#MyAppName}}"; Flags: nowait postinstall skipifsilent unchecked

[Registry]
Root: HKCU; Subkey: "Software\Classes\.bc"; ValueType: string; ValueName: ""; ValueData: "BiochamFile"; Flags: uninsdeletevalue
Root: HKCU; Subkey: "Software\Classes\BiochamFile"; ValueType: string; ValueName: ""; ValueData: "Biocham File"; Flags: uninsdeletekey
Root: HKCU; Subkey: "Software\Classes\BiochamFile\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\gui\{#MyAppExeName},0"
Root: HKCU; Subkey: "Software\Classes\BiochamFile\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\gui\{#MyAppExeName}"" ""{app}\gui"" ""%1"""
Root: HKLM; SubKey: "SYSTEM\CurrentControlSet\Control\Session Manager\Environment\"; ValueType: string; ValueName: "Path"; ValueData: "{reg:HKLM\SYSTEM\CurrentControlSet\Control\Session Manager\Environment\,Path};{app}"

[Setup]
ChangesEnvironment=yes

[Code]
function InitializeSetup(): Boolean;
var
   string : String;
begin
   string := '';
   if not RegQueryStringValue(HKEY_LOCAL_MACHINE, 'SOFTWARE\JavaSoft\Java Runtime Environment', 'CurrentVersion', string) then
      RegQueryStringValue(HKEY_LOCAL_MACHINE, 'SOFTWARE\JavaSoft\Java Development Kit', 'CurrentVersion', string);
   if (string = '') or (CompareStr(string, '1.5.0') < 0) then
      Result := MsgBox('Java >= 1.5 was not found on your computer' #13#13 'Do you really want to start setup?', mbConfirmation, MB_YESNO) = idYes
   else
      Result := true;
end;

procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);
var
  Path, AppDir: string;
  Index: Integer;
begin
  if CurUninstallStep = usUninstall then
  begin
    if RegQueryStringValue(HKEY_LOCAL_MACHINE,
      'SYSTEM\CurrentControlSet\Control\Session Manager\Environment\',
      'Path', Path) then
    begin
      AppDir := ExpandConstant('{app}');
      Index := Pos(AppDir, Path);
      Delete(Path, Index-1, Length(AppDir)+1);
      RegWriteStringValue(HKEY_LOCAL_MACHINE,
        'SYSTEM\CurrentControlSet\Control\Session Manager\Environment\',
        'Path', Path);
    end;
  end;
end;
